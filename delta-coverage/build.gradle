plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'

    id "pl.droidsonroids.jacoco.testkit" version "1.0.8"
}

gradlePlugin {

    website = 'https://github.com/SurpSG/delta-coverage'
    vcsUrl = 'https://github.com/SurpSG/delta-coverage.git'

    plugins {
        deltaCoveragePlugin {
            id = "io.github.surpsg.delta-coverage"
            displayName = "Delta Coverage"
            description = "Plugin that computes code coverage on modified code"
            implementationClass = "io.github.surpsg.deltacoverage.gradle.DeltaCoveragePlugin"
            tags.set(["differential", "diff", "delta", "coverage", "jacoco", "gradle", "plugin"])
        }
    }
}

shadowJar {
    archiveClassifier.set('')
}

ext {
    junit_version = '5.8.2'
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation 'org.assertj:assertj-core:3.20.2'
                implementation 'io.mockk:mockk:1.12.3'
                implementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
                implementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
                implementation "io.kotest:kotest-property-jvm:$kotest_version"
            }
        }

        functionalTest(JvmTestSuite) {
            testType = TestSuiteType.FUNCTIONAL_TEST
            useJUnitJupiter()

            sources {
                kotlin.srcDir file('src/funcTest/kotlin')
                resources.srcDir file('src/funcTest/resources')
            }
            gradlePlugin {
                testSourceSets sources
            }

            dependencies {
                implementation project()
                implementation 'org.eclipse.jgit:org.eclipse.jgit:5.12.0.202106070339-r'
                implementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
                implementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
            }
        }
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.named('check') {
    dependsOn(testing.suites.functionalTest)
}

tasks.named('functionalTest') {
    description = 'Runs the functional tests.'
    group = 'verification'
    dependsOn(generateJacocoFunctionalTestKitProperties)
}
jacocoTestKit {
    applyTo('functionalTestRuntimeOnly', tasks.named('functionalTest'))
}

dependencies {
    implementation project(':jacoco-filtering-extension')

    testImplementation gradleApi() // required to add this dependency explicitly after applying shadowJar plugin
}
